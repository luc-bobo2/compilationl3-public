Package sc;

Helpers

lettre = [['a' .. 'z'] + ['A' .. 'Z']];
chiffre = ['0' .. '9'];
alphanum = [lettre + chiffre];
alpha = [lettre + ['_' + '$']];

Tokens

espaces = (' ' | 13 | 10)+;
commentaire= '#' [[0 .. 0xffff] - [10 + 13]]* (10 | 13 | 10 13);

plus = '+';
moins = '-';
fois = '*';
diviser = '/';

ou = '|';
et = '&';
non = '!';
egal = '=';
inf = '<';

point_virgule = ';';
virgule = ',';
ouvrante = '(';
fermante = ')';
acco_open = '{';
acco_close = '}';
croch_open = '[';
croch_closed = ']';

si = 'si';
alors = 'alors';
sinon = 'sinon';
tantque = 'tantque';
faire = 'faire';
retour = 'retour';

entier = 'entier';
lire = 'lire';
ecrire = 'ecrire';

nombre = chiffre+;
id = alpha alphanum*;


Ignored Tokens

espaces, commentaire;

Productions

// Définition du programme
programme =
{avecvar} optdecvar listedecfonc |
{sansvar} listedecfonc;

// Liste des var globale du programme
optdecvar = listedecvar point_virgule;

// Liste de declarations de vars
listedecvar =
{decvarldecvar} decvar listedecvarbis |
{decvar} decvar;

listedecvarbis =
{decvarldecvar} virgule decvar listedecvarbis|
{decvar} virgule decvar ;

// Declaration d'une variable
decvar =
{decvarentier} entier id |
{decvartableau} entier id croch_open nombre croch_closed ;

// Liste de declarations de fonctions
listedecfonc =
{ldecfoncrec} decfonc listedecfonc |
{ldecfoncfinal} ;

// Declaration d'une fonction
decfonc =
{decvarinstr} id listeparam optdecvar instrblock |
{instr} id listeparam instrblock ;

// Liste des paramètre de la déclaration d'une fonction
listeparam =
{sansparam} ouvrante fermante |
{avecparam} ouvrante listedecvar fermante ;


// Les différentes instructions possibles
instr =
{affect} affect |
{blockinstr} instrblock |
{appelfct} instrappel |
{if} siblock |
{while} while |
{empty} instrvide |
{ecrire} ecriture |
{retouri} retourinstr;

// Liste d'instructions
listeinstr =
{linstrec} instr listeinstr |
{linstfinal};

// Instruction vide
instrvide = point_virgule;

// Instruction d'appel de fonction
instrappel = appelfct point_virgule ;

// Appel d'une  fonction
appelfct =
{param} id ouvrante listeexp fermante  |
{noparam} id ouvrante fermante  ;

// Instruction SI ALORS SINON
siblock =
{sinon} si exp alors instrblock sinonblock |
{si} si exp alors instrblock ;

sinonblock =
{sinon} sinon instrblock;

// Instruction TANT QUE
while =
{while} tantque exp faire instrblock;

// Instruction RETOUR
retourinstr =
{return} retour exp point_virgule;

// Instruction d'affectation
affect =
{affect} var egal exp point_virgule ;

// Bloc d'instructions
instrblock =
{blockinstr} acco_open listeinstr acco_close ;

// Instruction d'appel à ECRIRE
ecriture =
{fctecrire} ecrire ouvrante exp fermante point_virgule ;


// GRAMMAIRE DES DECLARATIONS DE VARIABLES ET FONCTIONS
// Appel d'une variable
var =
{tab} id croch_open exp croch_closed |
{simple} id;

// Declaration liste d'expressions
listeexp =
{recursif} exp bis|
{simple} exp;

bis =
{end} virgule exp |
{recursif} virgule exp bis;


// Expressions arithmétiques
exp =
{ou} exp ou e6 |
{e6} e6;

e6 =
{et} e6 et e5 |
{e5} e5;

e5 =
{egal} e5 egal e4 |
{inf} e5 inf e4 |
{e4} e4;

e4 =
{plus} e4 plus e3 |
{moins} e4 moins e3 |
{e3} e3;

e3 =
{fois} e3 fois e2 |
{diviser} e3 diviser e2 |
{e2} e2;

e2=
{excl} non e1 |
{e1} e1;

e1 =
{parenthese} ouvrante exp fermante |
{e0} e0;

e0 =
{number} nombre |
{appelfct} appelfct |
{lire} lire ouvrante fermante |
{variable} var;



